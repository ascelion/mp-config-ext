apply plugin: 'maven-publish'

def doPublish  = project.hasProperty( 'doPublish' )

def signKey    = "${publishName}.sign.keyId"
def signSecret = "${publishName}.sign.keySecret"
def bintrayUSR = "${publishName}.bintray.username"
def bintrayPWD = "${publishName}.bintray.password"

def gext       = gradle.ext

publishing {
	publications {
		maven( MavenPublication ) {
			pom {
				name = artifactId
				url = "https://github.com/ascelion/${publishName}"
				scm {
					url = "https://github.com/ascelion/${publishName}"
					connection = "https://github.com/ascelion/${publishName}.git"
					developerConnection = "git@github.com:ascelion/${publishName}.git"
				}
				licenses {
					license {
						name = 'GNU Affero General Public License version 3'
						url = 'https://opensource.org/licenses/AGPL-3.0'
						distribution = 'repo'
					}
				}
				withXml {
					def xml = it.asNode()
					def dm = xml.appendNode( 'dependencyManagement' ).appendNode( 'dependencies' )

					rootProject.subprojects { sub ->
						plugins.withType( MavenPublishPlugin ) {
							def dep = dm.appendNode 'dependency'

							dep.appendNode 'groupId', sub.group
							dep.appendNode 'artifactId', sub.name
							dep.appendNode 'version', sub.version
						}
					}
				}
			}
		}
	}

	if( doPublish && gradle.hasProperty( bintrayUSR ) ) {
		repositories {
			maven {
				name = 'jfrog'
				url = "https://oss.jfrog.org/artifactory/oss-${isSnapshot ? 'snapshot' : 'release'}-local"

				credentials {
					username = gext[bintrayUSR]
					password = gradle.decryptPassword( gext[bintrayPWD] )
				}
			}
		}
	}
}

if( gradle.hasProperty( signKey ) ) {
	apply plugin: 'signing'

	ext['signing.gnupg.keyName'] = gext[signKey]

	signing {
		useGpgCmd()
		sign publishing.publications.maven
	}
}
